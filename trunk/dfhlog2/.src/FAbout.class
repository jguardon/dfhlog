' Gambas class file
' dfhLog- Hamradio LogBook
'     Copyright (C) 2009  Jesús Guardón, EA7DFH <ea7dfh@gmail.com>
'
'     This program is free software: you can redistribute it and/or modify
'     it under the terms of the GNU General Public License as published by
'     the Free Software Foundation, either version 3 of the License, or
'     (at your option) any later version.
'
'     This program is distributed in the hope that it will be useful,
'     but WITHOUT ANY WARRANTY; without even the implied warranty of
'     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
'     GNU General Public License for more details.
'
'     You should have received a copy of the GNU General Public License
'     along with this program.  If not, see <http://www.gnu.org/licenses/>.

STATIC PRIVATE $aSupport AS String[]
STATIC PRIVATE $iWait AS Integer
STATIC PRIVATE $iSupport AS Integer

STATIC PUBLIC SUB Run()

    DIM hForm AS FAbout

    hForm = NEW FAbout
    hForm.ShowModal

END

PUBLIC SUB getCtyDat()

    DIM t, ctyd, y, m, d AS String
    DIM da AS Date
    DIM F AS File

    IF Exist(User.Home &/ ".config/dfhlog/cty.dat") THEN
        F = OPEN User.Home &/ ".config/dfhlog/cty.dat" FOR INPUT

        WHILE NOT Eof(F)
            LINE INPUT #F, t
            IF InStr(t, "=VER2") > 1 THEN
                ctyd = Mid(t, InStr(t, "=VER2") + 4, 8)
                y = Left(ctyd, 4)
                m = Mid(ctyd, 5, 2)
                d = Right(ctyd, 2)
                ctyd = d & "/" & m & "/" & y

                BREAK
            ENDIF
        WEND
        CLOSE #F
        lblCtyDat.Text = ("Cty.dat version: ") & ctyd

    ENDIF

END

PUBLIC SUB Form_Open()

    DIM sText AS String

    ' [GB2:BCOL] txtAuthor.BackColor = svwAuthor.BackColor
    ' [GB2:BCOL] txtAuthor.BackColor = svwAuthor.Background
    txtAuthor.Background = svwAuthor.Background

    GetVersion
    GetAuthor
    getCtyDat
    GetSupport

    txtGift.X = 0
    lblMessage.Text = "© 2008-" & Year(Now) & " Jesús Guardón, EA7DFH"
    '   panGift.ClientW
    '   sText = Trim(File.Load("support.txt"))
    '   txtGift.Text = "<b>" & ("Thanks to") & "</b>..." & Replace("\n" & sText, "\n", String$(8, "&nbsp;"))
    '   txtGift.W = txtGift.Font.Width(Replace(sText, "\n", "        ")) + 128

END

PRIVATE SUB GetVersion()

    lblVersion.Text = Application.Version
    ' [GB2:FNTW] lblVersion.X = lblGambas.X + lblGambas.Font.Width(lblGambas.Text)
    lblVersion.X = lblGambas.X + lblGambas.Font.TextWidth(lblGambas.Text)

END

PUBLIC SUB btnClose_Click()

    ME.Close

END

PUBLIC SUB timAuthor_Timer()

    DIM Y AS Integer

    Y = svwAuthor.ScrollY
    INC svwAuthor.ScrollY
    IF Y = svwAuthor.ScrollY THEN
        svwAuthor.ScrollY = 0
    ENDIF

    WITH txtGift

        IF $iWait > 0 THEN
            DEC $iWait
        ELSE IF .X > 0 THEN
            .X = Max(0, .X - 8)
            IF .X = 0 THEN $iWait = 50
        ELSE
            .X = panGift.Width
            txtGift.Text = $aSupport[$iSupport]
            INC $iSupport
            IF $iSupport >= $aSupport.Count THEN $iSupport = 0
        ENDIF

    END WITH

END

PRIVATE SUB RemoveLink(sStr AS String) AS String

    DIM iPos AS Integer
    DIM iPos2 AS Integer

    DO

        iPos = InStr(sStr, "<a href=")
        IF iPos = 0 THEN BREAK

        iPos2 = InStr(sStr, ">", iPos)
        IF iPos2 = 0 THEN iPos2 = Len(sStr) + 1
        sStr = Left(sStr, iPos - 1) & Mid(sStr, iPos2 + 1)

    LOOP

    RETURN Replace(sStr, "</a>", "")

END

PRIVATE SUB GetAuthor()

    DIM hFic AS File
    DIM sText AS String
    DIM sLig AS String
    DIM nLig AS Integer
    DIM sMail AS String
    DIM sCountry AS String

    hFic = OPEN "data/authors.txt"

    WHILE NOT Eof(hFic)

        LINE INPUT #hFic, sLig
        IF NOT sLig THEN CONTINUE
        IF Left(sLig) = "-" THEN CONTINUE

        LINE INPUT #hFic, sMail
        LINE INPUT #hFic, sCountry

        sText &= "<p><font size=+1><b>" & sLig & "</b>"
        IF sCountry THEN sText &= " (" & sCountry & ")"
        sText &= "</font></p><blockquote>\n"

        sText &= "<u>" & sMail & "</u><br>"

        WHILE NOT Eof(hFic)

            LINE INPUT #hFic, sLig
            IF NOT sLig THEN CONTINUE
            IF Left(sLig) = "-" THEN BREAK

            sLig = RemoveLink(sLig)
            IF Right(sLig) <> "." THEN sLig &= "."
            sText &= sLig & "<br>"

        WEND

        'IF Right$(sText, 4) = "<br>" THEN sText = Left$(sText, -4)

        sText = sText & "</blockquote>\n"

    WEND

    CLOSE #hFic

    txtAuthor.Text = sText
    'nLig * txtAuthor.Font.Height("a") * 0.95 + 256

FINALLY

    txtAuthor.Height = txtAuthor.TextHeight '+ 256
    'PRINT txtAuthor.TextHeight

CATCH

    txtAuthor.Text = Error.Text

END

PRIVATE SUB GetSupport()

    DIM iInd AS Integer

    $aSupport = Split(Trim(File.Load("data/support.txt")), "\n")

    FOR iInd = 0 TO $aSupport.Count - 1

        $aSupport[iInd] = "<b>" & $aSupport[iInd] & "...</b>"

    NEXT

    $aSupport.Add("<b>" & ("Thanks to") & "...</b>", 0)

END

PUBLIC SUB Form_Show()

    txtAuthor.Height = txtAuthor.TextHeight + 256

END

PUBLIC SUB btnGift_Click()

    Desktop.Open("http://ea7dfh.com/donate/")

END

PUBLIC SUB svwAuthor_Enter()

    timAuthor.Stop

END

PUBLIC SUB svwAuthor_Leave()

    timAuthor.Start

END

PUBLIC SUB Label1_MouseUp()

    Desktop.Open(Label1.Text)

END

PUBLIC SUB Label1_Enter()

    Label1.Mouse = Mouse.Pointing

END

PUBLIC SUB Label1_Leave()

    Label1.Mouse = Mouse.Default

END
